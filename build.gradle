plugins {
	id 'org.springframework.boot' version '2.1.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'com.moowork.node' version '1.3.1'
}

apply plugin: 'com.moowork.node'

group = 'com.github.CookieDuck'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://artifactory.global.dish.com/artifactory/repo'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.kafka:spring-kafka'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

node {
	version = '11.10.1'
	npmVersion = '5.6.0'
	download = false // using locally installed version due to proxy issues
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
	script = project.file('node_modules/.bin/webpack')
	args = [
		'--mode', 'development',
		'--entry', './src/main/ui/App.jsx',
		'-o', './src/main/resources/static/dist/react-app.js'
	]
}

processResources.dependsOn 'buildReactApp'
clean.delete << file('node_modules')
clean.delete << file('src/main/resources/static/dist')
